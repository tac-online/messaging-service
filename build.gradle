plugins {
    id 'java'
    id 'application'
    id 'com.bmuschko.docker-java-application' version '4.3.0'
}

mainClassName = 'de.johanneswirth.tac.messagingservice.MessagingServiceApp'

group 'de.johannes-wirth'
version '0.1'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
    mavenLocal()
}




dependencies {
    compile group: 'io.dropwizard', name: 'dropwizard-core', version: '1.3.15'
    compile group: 'io.dropwizard', name: 'dropwizard-client', version: '1.3.15'
    compile group: 'com.liveperson', name: 'dropwizard-websockets', version: '1.3.14'
    compile group: 'io.dropwizard.modules', name: 'dropwizard-discovery', version: '1.3.11'
    compile group: 'de.johannes-wirth', name: 'tac-backend-common', version: '0.1'
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.5.2'
}

jar {
    from(configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
    manifest { attributes 'Main-Class': mainClassName }
}

task copyScript(type: Copy) {
    from file("startup.sh")
    into file("build/install/")
}

task excludeLibraries(type: Delete) {
    delete fileTree(dir: "build/docker/${rootProject.name}/lib")
}
dockerBuildImage.dependsOn excludeLibraries
dockerBuildImage.dependsOn copyScript

docker {
    javaApplication {
        baseImage = 'java:openjdk-8-jre'
        maintainer = 'Johannes Wirth "johannes-wirth@posteo.de"'
        ports = [8080, 8081]
        tag = rootProject.name + ":" + version
        exec {
            defaultCommand 'startup.sh'
            entryPoint 'sh'
        }
    }
}


task tagImage(type:Exec) {
    dependsOn dockerBuildImage
    //on windows:
    commandLine 'cmd', '/c', 'docker tag ' + rootProject.name + ":" + version + " registry.johannes-wirth.de/" + rootProject.name + ":" + version
}

task publishImage(type:Exec) {
    dependsOn tagImage
    //on windows:
    commandLine 'cmd', '/c', 'docker push registry.johannes-wirth.de/' + rootProject.name + ":" + version
}


dockerCreateDockerfile.addFile('startup.sh', 'startup.sh')